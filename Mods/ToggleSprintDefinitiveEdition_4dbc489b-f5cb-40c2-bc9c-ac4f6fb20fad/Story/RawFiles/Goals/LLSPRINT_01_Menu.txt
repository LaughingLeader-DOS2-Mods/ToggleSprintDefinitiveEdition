Version 1
SubGoalCombiner SGC_AND
INITSECTION
/*
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_PartySprintingSpeed", "Party Sprinting Speed");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_WalkingSpeed", "Party Walking Speed");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_SprintingSpeedOverride", "Character Sprinting Speed Override");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_EnemySprintingSpeed", "NPC Sprinting Speed (In Combat)");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_EnemyWalkingSpeed", "NPC Walking Speed (In Combat)");
*/
KBSECTION
//REGION MENU_FLAG
IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_MenuFlags(_Flag, _MenuText)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _OtherMenuFlag)
THEN
NOT DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _OtherMenuFlag);

IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_MenuFlags(_Flag, _MenuText)
THEN
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _Flag);
DialogSetVariableStringForInstance(_Instance, "LLSPRINT_CurrentMenu_51342cd2-87c7-49a0-8ab5-94e19e5b2155", _MenuText);

IF
DialogEnded("ToggleSprint_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_ToggleSprint_MenuFlags(_Flag, _MenuText)
AND
ObjectGetFlag(_Player, _Flag, 1)
THEN
ObjectClearFlag(_Player, _Flag);

IF
DialogEnded("ToggleSprint_SettingsMenu", _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _MenuFlag)
THEN
NOT DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _MenuFlag);

IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _MenuFlag)
AND
StringContains(_MenuFlag, "Sprinting", 1)
AND
DB_ToggleSprint_SpeedDialogVars(_SelectFlag, _DialogVar)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
DialogSetVariableFloatForInstance(_Instance, _DialogVar, _SpeedVal);

IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _MenuFlag)
AND
StringContains(_MenuFlag, "Walking", 1)
AND
DB_ToggleSprint_SpeedDialogVars(_SelectFlag, _DialogVar)
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
DialogSetVariableFloatForInstance(_Instance, _DialogVar, _SpeedVal);

IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _MenuFlag)
AND
StringContains(_MenuFlag, "Sprinting", 1)
AND
DB_ToggleSprint_DefaultSprintingSpeed(_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_DefaultSpeed_d3f971ea-c465-45d4-ab08-27593e1e9ac4", _SpeedVal);

IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _MenuFlag)
AND
StringContains(_MenuFlag, "Walking", 1)
AND
DB_ToggleSprint_DefaultWalkingSpeed(_Flag)
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_DefaultSpeed_d3f971ea-c465-45d4-ab08-27593e1e9ac4", _SpeedVal);

IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _MenuFlag)
AND
StringContains(_MenuFlag, "Sprinting", 1)
THEN
DialogSetVariableStringForInstance(_Instance, "LLSPRINT_DOS2_DefaultSpeed_e21bd799-d097-434e-9967-68ad9d9ff09e", "3.75");

IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _MenuFlag)
AND
StringContains(_MenuFlag, "Walking", 1)
THEN
DialogSetVariableStringForInstance(_Instance, "LLSPRINT_DOS2_DefaultSpeed_e21bd799-d097-434e-9967-68ad9d9ff09e", "2.0");
//END_REGION

//REGION CURRENT_SPEED_VARIABLE_SET
IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_PartySprintingSpeed")
AND
DB_ToggleSprint_PartySprintingSpeed(_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
DebugBreak(_Flag);
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);

IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_WalkingSpeed")
AND
GetVarFloat(_Character, "ToggleSprint_WalkSpeed", _SpeedVal)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);

IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_WalkingSpeed")
AND
NOT GetVarFloat(_Character, "ToggleSprint_WalkSpeed", _)
AND
DB_ToggleSprint_DefaultWalkingSpeed(_Flag)
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);

IF
ObjectFlagCleared("ToggleSprint_OverridingPartySpeed", (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_SprintingSpeedOverride")
AND
DB_ToggleSprint_PartySprintingSpeed(_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);

IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_SprintingSpeedOverride")
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
AND
ObjectGetFlag(_Character, _Flag, 1)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);

IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_EnemySprintingSpeed")
AND
DB_ToggleSprint_EnemySprintingSpeed(_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);

IF
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_EnemyWalkingSpeed")
AND
DB_ToggleSprint_EnemyWalkingSpeed(_Flag)
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);
//END_REGION

//REGION PARTY_SPEED_FLAGS
IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_PartySprintingSpeed")
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);

IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_PartySprintingSpeed")
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
LLSPRINT_SetPartySpeed(_Flag);

IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_WalkingSpeed")
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);

IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_WalkingSpeed")
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
LLSPRINT_SetPartyWalkSpeed(_Flag);
//END_REGION

//REGION SPRINTING_SPEED_FLAGS
IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
AND
ObjectGetFlag(_Character, "ToggleSprint_SprintingEnabled", 1)
THEN
LLSPRINT_SetSpeed(_Character, _SpeedVal, 1);

IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Character, _)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
LLSPRINT_ClearLastSpeedFlag(_Character, _Flag);
SetVarString(_Character, "ToggleSprint_CurrentSpeedFlag", _Flag);

IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_PartySprintingSpeed")
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
AND
DB_DialogName("ToggleSprint_SettingsMenu", _Instance)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);
//END_REGION

//REGION SPRINTING_SPEED_OVERRIDE_FLAGS
IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_SprintingSpeedOverride")
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
ObjectSetFlag(_Character, "ToggleSprint_OverridingPartySpeed");
ObjectSetFlag(_Character, _Flag);

IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_SprintingSpeedOverride")
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
AND
DB_DialogName("ToggleSprint_SettingsMenu", _Instance)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);
//END_REGION

//REGION ENEMY_SPEED_FLAGS
IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_EnemySprintingSpeed")
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
SysClear("DB_ToggleSprint_EnemySprintingSpeed", 1);
DB_ToggleSprint_EnemySprintingSpeed(_Flag);

IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_EnemySprintingSpeed")
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
AND
DB_DialogName("ToggleSprint_SettingsMenu", _Instance)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);

IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_EnemyWalkingSpeed")
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
SysClear("DB_ToggleSprint_EnemyWalkingSpeed", 1);
DB_ToggleSprint_EnemyWalkingSpeed(_Flag);

IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, "LLSPRINT_Menu_EnemyWalkingSpeed")
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedVal)
AND
DB_DialogName("ToggleSprint_SettingsMenu", _Instance)
THEN
DialogSetVariableFloatForInstance(_Instance, "LLSPRINT_CurrentSpeed_ababef8c-c917-4fc2-8140-4d154cf6b49c", _SpeedVal);
//END_REGION

//REGION CLEAR_SELECT_FLAG
IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_SpeedDialogVars(_SelectFlag, _DialogVar)
THEN
ObjectClearFlag(_Character, _SelectFlag);
//END_REGION

//REGION HOST_FLAG
PROC
LLSPRINT_Menu_SetHostFlag()
AND
CharacterGetHostCharacter(_Host)
AND
DB_IsPlayer(_Player)
AND
_Player != _Host
AND
UserGetFlag(_Player, "LLSPRINT_IsHost", 1)
AND
NOT LLSPRINT_QRY_UserMatch(_Player, _Host)
THEN
UserClearFlag(_Player, "LLSPRINT_IsHost", 0);

PROC
LLSPRINT_Menu_SetHostFlag()
AND
CharacterGetHostCharacter(_Host)
AND
UserGetFlag(_Host, "LLSPRINT_IsHost", 0)
THEN
UserSetFlag(_Host, "LLSPRINT_IsHost", 0);

QRY
LLSPRINT_QRY_UserMatch((CHARACTERGUID)_Char, (CHARACTERGUID)_Char2)
AND
CharacterGetReservedUserID(_Char, _UserID)
AND
CharacterGetReservedUserID(_Char2, _UserID)
THEN
DB_NOOP(1);
//END_REGION

//REGION SETTINGS_MENU_OPENING
PROC
ProcBlockUseOfItem(_Character, _Item)
AND
GetTemplate(_Item, "BOOK_Book_LLSPRINT_Settings_54bb6a3e-1b5c-4e95-9f9b-06d87f3e7715")
AND
CharacterIsInCombat(_Character, 1)
THEN
CharacterStatusText(_Character, "LLSPRINT_StatusText_SettingsBlocked");
DB_CustomUseItemResponse(_Character, _Item, 0);

QRY
LLSPRINT_Menu_QRY_OpenSetup((CHARACTERGUID)_Character)
AND
GlobalGetFlag("ToggleSprint_FirstTimeSetupComplete", 0)
AND
UserGetFlag(_Character, "LLSPRINT_IsHost", 1)
THEN
DB_NOOP(1);

IF
CharacterUsedItemTemplate(_Character, "BOOK_Book_LLSPRINT_Settings_54bb6a3e-1b5c-4e95-9f9b-06d87f3e7715", _Item)
THEN
LLSPRINT_Menu_SetHostFlag();
LLSPRINT_Menu_OpenMenu(_Character);

IF
DialogEnded("ToggleSprint_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Character)
AND
ObjectGetFlag(_Character, "ToggleSprint_OpenSetup", 1)
THEN
ObjectClearFlag(_Character, "ToggleSprint_OpenSetup");
Proc_StartDialog(0, "ToggleSprint_Setup", _Character, _Character);

IF
DialogEnded("ToggleSprint_Setup", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Character)
AND
ObjectGetFlag(_Character, "ToggleSprint_OpenSettingsMenu", 1)
THEN
ObjectClearFlag(_Character, "ToggleSprint_OpenSettingsMenu");
Proc_StartDialog(0, "ToggleSprint_SettingsMenu", _Character, _Character);

IF
StoryEvent((CHARACTERGUID)_Character, "ToggleSprint_SettingsMenu_OpenRequest")
THEN
LLSPRINT_Menu_SetHostFlag();
LLSPRINT_Menu_OpenMenu(_Character);

PROC
LLSPRINT_Menu_OpenMenu((CHARACTERGUID)_Character)
AND
LLSPRINT_Menu_QRY_OpenSetup(_Character)
THEN
Proc_StartDialog(0, "ToggleSprint_Setup", _Character, _Character);

PROC
LLSPRINT_Menu_OpenMenu((CHARACTERGUID)_Character)
AND
NOT LLSPRINT_Menu_QRY_OpenSetup(_Character)
THEN
Proc_StartDialog(0, "ToggleSprint_SettingsMenu", _Character, _Character);
//END_REGION

//REGION SETTINGS_MENU_COMMANDS
IF
ObjectFlagSet("LLSPRINT_Commands_ResetPartySpeed", (CHARACTERGUID)_Character, _Instance)
AND
LLSPRINT_QRY_ClearFlag(_Character, "LLSPRINT_Commands_ResetPartySpeed")
AND
DB_ToggleSprint_DefaultSprintingSpeed(_Flag)
THEN
LLSPRINT_SetPartySpeed(_Flag);

IF
ObjectFlagSet("LLSPRINT_Commands_AddSprintSkill", (CHARACTERGUID)_Character, _Instance)
THEN
ObjectClearFlag(_Character, "LLSPRINT_Commands_AddSprintSkill");
CharacterAddSkill(_Character, "Shout_LLSPRINT_ToggleSprint", 1);

IF
ObjectFlagSet("LLSPRINT_Commands_RemoveSprintSkill", (CHARACTERGUID)_Character, _Instance)
THEN
ObjectClearFlag(_Character, "LLSPRINT_Commands_RemoveSprintSkill");
CharacterRemoveSkill(_Character, "Shout_LLSPRINT_ToggleSprint");

IF
ObjectFlagSet("LLSPRINT_Commands_AddSettingsBook", (CHARACTERGUID)_Character, _Instance)
THEN
ItemTemplateAddTo("BOOK_Book_LLSPRINT_Settings_54bb6a3e-1b5c-4e95-9f9b-06d87f3e7715", _Character, 1, 1);

QRY
LLSPRINT_QRY_ClearFlag((GUIDSTRING)_Object, (STRING)_Flag)
THEN
ObjectClearFlag(_Object, _Flag);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_ToggleSprintDE"
