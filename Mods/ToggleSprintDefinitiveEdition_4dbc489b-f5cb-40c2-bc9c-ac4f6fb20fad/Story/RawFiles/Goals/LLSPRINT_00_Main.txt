Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLSPRINT_InitSettings();
LLSPRINT_Internal_RegisterMod();

/*Settings*/
//DB_ToggleSprint_SpeedDialogVars(_SelectFlag, _DialogVar)
//DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
//DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedVal)
//DB_ToggleSprint_MenuFlags(_Flag, _MenuText)
//DB_ToggleSprint_DefaultSprintingSpeed(_Flag)
//DB_ToggleSprint_DefaultWalkingSpeed(_Flag)
/*VARS*/
//DB_ToggleSprint_PartySprintingSpeed(_Flag)
//DB_ToggleSprint_EnemySprintingSpeed(_Flag)
//DB_ToggleSprint_EnemyWalkingSpeed(_Flag)
/*TEMP*/
//DB_ToggleSprint_Temp_CurrentMenu(_Instance, _Character, _Flag)
/*
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_PartySprintingSpeed", "Party Sprinting Speed");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_WalkingSpeed", "Party Walking Speed");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_SprintingSpeedOverride", "Character Sprinting Speed Override");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_EnemySprintingSpeed", "NPC Sprinting Speed (In Combat)");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_EnemyWalkingSpeed", "NPC Walking Speed (In Combat)");
*/
KBSECTION
//REGION SETTINGS
PROC
LLSPRINT_InitSettings()
AND
NOT DB_ToggleSprint_SpeedDialogVars(_,_)
THEN
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_00", "LLSPRINT_Speed00_c9ddbb10-7213-48e2-904a-2b7ec049beef");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_01", "LLSPRINT_Speed01_466add38-bab6-446c-90f0-9f067886a734");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_02", "LLSPRINT_Speed02_5c20442d-b73a-45f0-a22a-569958263c1b");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_03", "LLSPRINT_Speed03_c176cda1-3ce5-401d-bcba-10d9f136e3ec");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_04", "LLSPRINT_Speed04_885b4609-c38d-46d4-bb5a-e61087b58280");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_05", "LLSPRINT_Speed05_f5c50ba6-f41f-40a0-951a-9bb97d0569ad");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_06", "LLSPRINT_Speed06_fcacc826-a01b-429e-8247-cf03d4eee922");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_07", "LLSPRINT_Speed07_b21b24c7-6a19-4f69-a852-0820ae31e8c8");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_08", "LLSPRINT_Speed08_fc6eaba6-f64f-4acf-83d7-167c1acf082f");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_09", "LLSPRINT_Speed09_4893730f-1deb-41a4-bae3-d7e7ed3d67f5");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_10", "LLSPRINT_Speed10_4efd0366-0bc5-405a-8861-038778201a2a");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_11", "LLSPRINT_Speed11_ca8453d5-8330-4148-98e5-04f93b424ccb");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_12", "LLSPRINT_Speed12_1c2839f9-3e48-48e7-8b60-b159ce2e64b7");
DB_ToggleSprint_SpeedDialogVars("ToggleSprint_SelectSpeed_13", "LLSPRINT_Speed13_c9a462e0-07cc-4d2f-bb15-b1119739ab21");

PROC
LLSPRINT_InitSettings()
AND
NOT DB_ToggleSprint_SprintingSpeed(_,_,_)
THEN
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_00", "ToggleSprint_SprintingSpeed_00", 4.0);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_01", "ToggleSprint_SprintingSpeed_01", 4.5);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_02", "ToggleSprint_SprintingSpeed_02", 5.0);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_03", "ToggleSprint_SprintingSpeed_03", 5.5);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_04", "ToggleSprint_SprintingSpeed_04", 6.0);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_05", "ToggleSprint_SprintingSpeed_05", 6.5);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_06", "ToggleSprint_SprintingSpeed_06", 7.0);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_07", "ToggleSprint_SprintingSpeed_07", 7.5);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_08", "ToggleSprint_SprintingSpeed_08", 8.0);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_09", "ToggleSprint_SprintingSpeed_09", 8.5);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_10", "ToggleSprint_SprintingSpeed_10", 9.0);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_11", "ToggleSprint_SprintingSpeed_11", 9.5);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_12", "ToggleSprint_SprintingSpeed_12", 10.0);
DB_ToggleSprint_SprintingSpeed("ToggleSprint_SelectSpeed_13", "ToggleSprint_SprintingSpeed_13", 11.0);

PROC
LLSPRINT_InitSettings()
AND
NOT DB_ToggleSprint_DefaultSprintingSpeed(_)
THEN
DB_ToggleSprint_DefaultSprintingSpeed("ToggleSprint_SprintingSpeed_02");

PROC
LLSPRINT_InitSettings()
AND
NOT DB_ToggleSprint_PartySprintingSpeed(_)
AND
DB_ToggleSprint_DefaultSprintingSpeed(_Flag)
THEN
LLSPRINT_SetPartySpeed(_Flag);

PROC
LLSPRINT_InitSettings()
AND
NOT DB_ToggleSprint_WalkingSpeed(_,_,_)
THEN
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_00", "ToggleSprint_WalkingSpeed_00", 2.0);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_01", "ToggleSprint_WalkingSpeed_01", 2.5);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_02", "ToggleSprint_WalkingSpeed_02", 3.0);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_03", "ToggleSprint_WalkingSpeed_03", 3.5);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_04", "ToggleSprint_WalkingSpeed_04", 4.0);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_05", "ToggleSprint_WalkingSpeed_05", 4.5);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_06", "ToggleSprint_WalkingSpeed_06", 5.0);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_07", "ToggleSprint_WalkingSpeed_07", 5.5);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_08", "ToggleSprint_WalkingSpeed_08", 6.0);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_09", "ToggleSprint_WalkingSpeed_09", 6.5);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_10", "ToggleSprint_WalkingSpeed_10", 7.0);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_11", "ToggleSprint_WalkingSpeed_11", 7.5);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_12", "ToggleSprint_WalkingSpeed_12", 8.0);
DB_ToggleSprint_WalkingSpeed("ToggleSprint_SelectSpeed_13", "ToggleSprint_WalkingSpeed_13", 9.0);

PROC
LLSPRINT_InitSettings()
AND
NOT DB_ToggleSprint_DefaultWalkingSpeed(_)
THEN
DB_ToggleSprint_DefaultWalkingSpeed("ToggleSprint_WalkingSpeed_00");

PROC
LLSPRINT_InitSettings()
AND
NOT DB_ToggleSprint_MenuFlags(_,_)
THEN
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_PartySprintingSpeed", "Party Sprinting Speed");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_WalkingSpeed", "Party Walking Speed");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_SprintingSpeedOverride", "Character Sprinting Speed Override");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_EnemySprintingSpeed", "NPC Sprinting Speed (In Combat)");
DB_ToggleSprint_MenuFlags("LLSPRINT_Menu_EnemyWalkingSpeed", "NPC Walking Speed (In Combat)");

PROC
LLSPRINT_InitSettings()
AND
NOT DB_ToggleSprint_EnemySprintingSpeed(_)
AND
DB_ToggleSprint_DefaultSprintingSpeed(_Flag)
THEN
DB_ToggleSprint_EnemySprintingSpeed(_Flag);

PROC
LLSPRINT_InitSettings()
AND
NOT DB_ToggleSprint_EnemyWalkingSpeed(_)
AND
DB_ToggleSprint_DefaultWalkingSpeed(_Flag)
THEN
DB_ToggleSprint_EnemyWalkingSpeed(_Flag);

PROC
LLSPRINT_Internal_ResetStaticDatabases()
THEN
SysClear("DB_ToggleSprint_SpeedDialogVars", 2);
SysClear("DB_ToggleSprint_SprintingSpeed", 3);
SysClear("DB_ToggleSprint_WalkingSpeed", 3);
SysClear("DB_ToggleSprint_MenuFlags", 2);
SysClear("DB_ToggleSprint_DefaultSprintingSpeed", 1);
SysClear("DB_ToggleSprint_DefaultWalkingSpeed", 1);
LLSPRINT_InitSettings();

//Disabled for now
PROC
LLSPRINT_Updater_VersionUpdated((STRING)_Version, (STRING)_NextVersion)
AND
_Version == "-1"
THEN
LLSPRINT_Internal_ResetStaticDatabases();
//END_REGION

//REGION INITIALIZE_SPRINTING
PROC
LLSPRINT_Updater_VersionUpdated((STRING)_Version, (STRING)_NextVersion)
AND
NOT GlobalGetFlag("ToggleSprint_Initialized", 1)
THEN
DebugBreak("[LLSPRINT:VersionUpdated] [ERROR] Toggle Sprint was never initialized on an existing save. Re-initializing.");
LLSPRINT_Internal_ResetStaticDatabases(); // Just in case
GlobalSetFlag("ToggleSprint_Initialized");
TimerCancel("ToggleSprint_Timers_Initialize");
TimerLaunch("ToggleSprint_Timers_Initialize", 500);

IF
GameStarted(_Level, _)
AND
IsGameLevel(_Level, 1)
AND
NOT GlobalGetFlag("ToggleSprint_Initialized", 1)
THEN
GlobalSetFlag("ToggleSprint_Initialized");
TimerLaunch("ToggleSprint_Timers_Initialize", 500);

IF
TimerFinished("ToggleSprint_Timers_Initialize")
AND
NOT DB_ToggleSprint_PartySprintingSpeed(_)
AND
DB_ToggleSprint_DefaultSprintingSpeed(_Flag)
THEN
LLSPRINT_SetPartySpeed(_Flag);

IF
TimerFinished("ToggleSprint_Timers_Initialize")
THEN
IterateParties("ToggleSprint_Events_SetInitialSprinting");

IF
StoryEvent((CHARACTERGUID)_Player, "ToggleSprint_Events_SetInitialSprinting")
AND
GlobalGetFlag("LeaderLib_AutoAddModMenuBooksDisabled", 0)
AND
NOT QRY_IsSummonOrPartyFollower(_Player)
AND
ItemTemplateIsInUserInventory(_Player, "BOOK_Book_LLSPRINT_Settings_54bb6a3e-1b5c-4e95-9f9b-06d87f3e7715", 0, 0)
THEN
ItemTemplateAddTo("BOOK_Book_LLSPRINT_Settings_54bb6a3e-1b5c-4e95-9f9b-06d87f3e7715", _Player, 1, 0);

IF
StoryEvent((CHARACTERGUID)_Player, "ToggleSprint_Events_SetInitialSprinting")
AND
GlobalGetFlag("ToggleSprint_AutoAddSprintSkillDisabled", 0)
AND
NOT QRY_IsSummonOrPartyFollower(_Player)
AND
NOT LLSPRINT_QRY_IgnoreCharacter(_Player)
AND
CharacterHasSkill(_Player, "Shout_LLSPRINT_ToggleSprint", 0)
AND
NOT ObjectGetFlag(_Player, "LLSPRINT_Debug_RemovedSprintSkill", 1) // In case it was removed manually by the player.
THEN
CharacterAddSkill(_Player, "Shout_LLSPRINT_ToggleSprint", 0);

IF
StoryEvent((CHARACTERGUID)_Player, "ToggleSprint_Events_SetInitialSprinting")
AND
NOT QRY_IsSummonOrPartyFollower(_Player)
AND
DB_ToggleSprint_PartySprintingSpeed(_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
LLSPRINT_SetSpeed(_Player, _SpeedVal);
LLSPRINT_ToggleSprinting(_Player, 1);
//END_REGION

//REGION QUERIES
QRY
LLSPRINT_QRY_SpeedVariableSet((CHARACTERGUID)_Character)
AND
GetVarFloat(_Character, "ToggleSprint_SprintSpeed", _SprintSpeed)
AND
_SprintSpeed > 0
THEN
DB_NOOP(1);

QRY
LLSPRINT_QRY_SpeedFlagVariableSet((CHARACTERGUID)_Character)
AND
GetVarString(_Character, "ToggleSprint_CurrentSpeedFlag", _CurrentFlag)
AND
_CurrentFlag != ""
THEN
DB_NOOP(1);

QRY
LLSPRINT_QRY_HasSpeedFlag((CHARACTERGUID)_Character)
AND
GetVarString(_Character, "ToggleSprint_CurrentSpeedFlag", _CurrentFlag)
AND
_CurrentFlag != ""
THEN
DB_NOOP(1);

QRY
LLSPRINT_QRY_CanSprint((CHARACTERGUID)_Character)
AND
CharacterIsInCombat(_Character, 1)
AND
GlobalGetFlag("ToggleSprint_CombatSprintingEnabled", 1)
THEN
DB_NOOP(1);

QRY
LLSPRINT_QRY_CanSprint((CHARACTERGUID)_Character)
AND
CharacterIsInCombat(_Character, 0)
THEN
DB_NOOP(1);

QRY
LLSPRINT_QRY_UserIsSprinting((CHARACTERGUID)_Character)
AND
CharacterGetReservedUserID(_Character, _User)
AND
DB_IsPlayer(_Player)
AND
CharacterGetReservedUserID(_Player, _User)
AND
ObjectGetFlag(_Player, "ToggleSprint_SprintingEnabled", 1)
THEN
DB_NOOP(1);

QRY
LLSPRINT_QRY_CharacterIsSprinting((CHARACTERGUID)_Character)
AND
ObjectGetFlag(_Character, "ToggleSprint_SprintingEnabled", 1)
THEN
DB_NOOP(1);

/*
QRY
LLSPRINT_QRY_CharacterIsSprinting((CHARACTERGUID)_Character)
AND
HasActiveStatus(_Character, "LLSPRINT_TOGGLESPRINT", 1)
THEN
DB_NOOP(1);
*/

QRY
LLSPRINT_QRY_ShouldBeSprinting((CHARACTERGUID)_Character)
AND
ObjectGetFlag(_Character, "ToggleSprint_SprintingEnabled", 0)
THEN
DB_NOOP(1);

/*
QRY
LLSPRINT_QRY_ShouldBeSprinting((CHARACTERGUID)_Character)
AND
HasActiveStatus(_Character, "LLSPRINT_TOGGLESPRINT", 0)
THEN
DB_NOOP(1);
*/

QRY
LLSPRINT_QRY_IgnoreCharacter((CHARACTERGUID)_Character)
AND
CharacterIsPlayer(_Character, 0)
THEN
DB_NOOP(1);

QRY
LLSPRINT_QRY_IgnoreCharacter((CHARACTERGUID)_Character)
AND
CharacterIsSummon(_Character, 1)
AND
NOT UserGetFlag(_Character, "ToggleSprint_AddSkillToSummons", 1)
THEN
DB_NOOP(1);
//END_REGION

//REGION SPEED_PROCS
PROC
LLSPRINT_ClearLastSpeedFlag((CHARACTERGUID)_Character, (STRING)_NextFlag)
AND
GetVarString(_Character, "ToggleSprint_CurrentSpeedFlag", _CurrentFlag)
AND
_NextFlag != _CurrentFlag
THEN
ObjectClearFlag(_Character, _CurrentFlag);

PROC
LLSPRINT_SetSpeed((CHARACTERGUID)_Character, (REAL)_SpeedVal)
THEN
LLSPRINT_SetSpeed(_Character, _SpeedVal, 0);

PROC
LLSPRINT_SetSpeed((CHARACTERGUID)_Character, (REAL)_SpeedVal, (INTEGER)_UpdateSpeed)
THEN
SetVarFloat(_Character, "ToggleSprint_SprintSpeed", _SpeedVal);

PROC
LLSPRINT_SetSpeed((CHARACTERGUID)_Character, (REAL)_SpeedVal, 1)
THEN
SetStoryEvent(_Character, "ToggleSprint_Commands_UpdateSpeed");
//END_REGION

//REGION SPEED_OVERRIDE_RESET
IF
ObjectFlagCleared("ToggleSprint_OverridingPartySpeed", (CHARACTERGUID)_Character, _Instance)
AND
DB_ToggleSprint_PartySprintingSpeed(_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
ObjectSetFlag(_Character, _Flag);
//END_REGION

//REGION CLIMBING_SPEED
IF
CharacterStatusApplied(_Char, "CLIMBING", _)
AND
ObjectGetFlag(_Char, "ToggleSprint_SprintingEnabled", 1)
AND
GlobalGetFlag("ToggleSprint_LadderSprintingDisabled", 0)
AND
CharacterIsInCombat(_Char, 0)
AND
GlobalGetFlag("ToggleSprint_FasterLadderSprinting", _SuperSpeed)
THEN
LLSPRINT_ApplyLadderSprinting(_Char, _SuperSpeed);

PROC
LLSPRINT_ApplyLadderSprinting((CHARACTERGUID)_Char, 0)
THEN
ApplyStatus(_Char, "LLSPRINT_LADDERSPRINT", 6.0, 1, _Char);

PROC
LLSPRINT_ApplyLadderSprinting((CHARACTERGUID)_Char, 1)
THEN
ApplyStatus(_Char, "LLSPRINT_LADDERSPRINT_FASTER", 6.0, 1, _Char);

IF
CharacterStatusRemoved(_Char, "CLIMBING", _)
AND
HasActiveStatus(_Char, "LLSPRINT_LADDERSPRINT", _a)
AND
HasActiveStatus(_Char, "LLSPRINT_LADDERSPRINT_FASTER", _b)
AND
IntegerMax(_a, _b, 1)
THEN
RemoveStatus(_Char, "LLSPRINT_LADDERSPRINT");
RemoveStatus(_Char, "LLSPRINT_LADDERSPRINT_FASTER");
//END_REGION

//REGION PARTY_EVENTS
IF
CharacterJoinedParty(_Character)
AND
DB_ToggleSprint_PartySprintingSpeed(_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
LLSPRINT_SetSpeed(_Character, _SpeedVal);
SetVarString(_Character, "ToggleSprint_CurrentSpeedFlag", _Flag);

IF
CharacterJoinedParty(_Character)
AND
LLSPRINT_QRY_UserIsSprinting(_Character)
THEN
LLSPRINT_ToggleSprinting(_Character, 1);

IF
CharacterJoinedParty(_Character)
AND
GlobalGetFlag("ToggleSprint_AutoAddSprintSkillDisabled", 0)
AND
NOT LLSPRINT_QRY_IgnoreCharacter(_Character)
AND
NOT ObjectGetFlag(_Character, "LLSPRINT_Debug_RemovedSprintSkill", 1)
AND
CharacterHasSkill(_Character, "Shout_LLSPRINT_ToggleSprint", 0)
THEN
CharacterAddSkill(_Character, "Shout_LLSPRINT_ToggleSprint", 0);

IF
CharacterJoinedParty(_Character)
AND
GlobalGetFlag("LeaderLib_AutoAddModMenuBooksDisabled", 0)
AND
NOT LLSPRINT_QRY_IgnoreCharacter(_Character)
AND
ItemTemplateIsInUserInventory(_Character, "BOOK_Book_LLSPRINT_Settings_54bb6a3e-1b5c-4e95-9f9b-06d87f3e7715", 0, 0)
AND
CharacterIsControlled(_Character, 1)
THEN
ItemTemplateAddTo("BOOK_Book_LLSPRINT_Settings_54bb6a3e-1b5c-4e95-9f9b-06d87f3e7715", _Character, 1, 0);

IF
CharacterLeftParty(_Character)
AND
NOT CharacterIsControlled(_Character, 1)
AND
ObjectGetFlag(_Character, "ToggleSprint_SprintingEnabled", 1)
THEN
LLSPRINT_ToggleSprinting(_Character, 0);

PROC
LLSPRINT_StorePartySpeed((STRING)_Flag)
AND
DB_ToggleSprint_PartySprintingSpeed(_OtherFlag)
THEN
NOT DB_ToggleSprint_PartySprintingSpeed(_OtherFlag);

PROC
LLSPRINT_StorePartySpeed((STRING)_Flag)
THEN
DB_ToggleSprint_PartySprintingSpeed(_Flag);

PROC
LLSPRINT_SetPartySpeed((STRING)_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
AND
CharacterGetHostCharacter(_Host)
THEN
LLSPRINT_StorePartySpeed(_Flag);
PartySetFlag(_Host, _Flag);

PROC
LLSPRINT_SetPartySpeed((STRING)_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
AND
DB_IsPlayer(_Player)
AND
NOT ObjectGetFlag(_Player, "ToggleSprint_OverridingPartySpeed", 1)
THEN
LLSPRINT_ClearLastSpeedFlag(_Player, _Flag);
SetVarString(_Player, "ToggleSprint_CurrentSpeedFlag", _Flag);
ObjectSetFlag(_Player, _Flag);

PROC
LLSPRINT_StorePartyWalkSpeed((STRING)_Flag)
AND
DB_ToggleSprint_PartyWalkingSpeed(_OtherFlag)
THEN
NOT DB_ToggleSprint_PartyWalkingSpeed(_OtherFlag);

PROC
LLSPRINT_StorePartyWalkSpeed((STRING)_Flag) 
THEN
DB_ToggleSprint_PartyWalkingSpeed(_Flag);

PROC
LLSPRINT_SetPartyWalkSpeed((STRING)_Flag)
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedVal)
AND
CharacterGetHostCharacter(_Host)
THEN
LLSPRINT_StorePartyWalkSpeed(_Flag);
PartySetFlag(_Host, _Flag);

PROC
LLSPRINT_SetPartyWalkSpeed((STRING)_Flag)
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedVal)
AND
DB_IsPlayer(_Player)
THEN
SetVarFloat(_Player, "ToggleSprint_WalkSpeed", _SpeedVal);
ObjectSetFlag(_Player, _Flag);
SetStoryEvent(_Player, "ToggleSprint_SetWalkingSpeed");
//END_REGION

//REGION TOGGLE_SKILL
IF
CharacterUsedSkill(_Character, "Shout_LLSPRINT_ToggleSprint", _, _)
THEN
LLSPRINT_ToggleSprinting(_Character);

PROC
LLSPRINT_ToggleSprinting((CHARACTERGUID)_Character)
AND
LLSPRINT_QRY_CanSprint(_Character)
AND
NOT DB_LLSPRINT_SprintStatusToggled(_Character)
AND
LLSPRINT_QRY_ShouldBeSprinting(_Character)
THEN
ObjectSetFlag(_Character, "ToggleSprint_SprintingEnabled");
DB_LLSPRINT_SprintStatusToggled(_Character);

PROC
LLSPRINT_ToggleSprinting((CHARACTERGUID)_Character)
AND
LLSPRINT_QRY_CanSprint(_Character)
AND
NOT DB_LLSPRINT_SprintStatusToggled(_Character)
AND
LLSPRINT_QRY_CharacterIsSprinting(_Character)
THEN
ObjectClearFlag(_Character, "ToggleSprint_SprintingEnabled");
DB_LLSPRINT_SprintStatusToggled(_Character);

PROC
LLSPRINT_ToggleSprinting((CHARACTERGUID)_Character)
AND
DB_LLSPRINT_SprintStatusToggled(_Character)
THEN
NOT DB_LLSPRINT_SprintStatusToggled(_Character);

PROC
LLSPRINT_ToggleSprinting((CHARACTERGUID)_Character, 1)
AND
LLSPRINT_QRY_CanSprint(_Character)
AND
NOT DB_LLSPRINT_SprintStatusToggled(_Character)
AND
LLSPRINT_QRY_ShouldBeSprinting(_Character)
THEN
ObjectSetFlag(_Character, "ToggleSprint_SprintingEnabled");
DB_LLSPRINT_SprintStatusToggled(_Character);

PROC
LLSPRINT_ToggleSprinting((CHARACTERGUID)_Character, 0)
AND
NOT DB_LLSPRINT_SprintStatusToggled(_Character)
AND
LLSPRINT_QRY_CharacterIsSprinting(_Character)
THEN
ObjectClearFlag(_Character, "ToggleSprint_SprintingEnabled");
DB_LLSPRINT_SprintStatusToggled(_Character);

PROC
LLSPRINT_ToggleSprinting((CHARACTERGUID)_Character, (INTEGER)_Enable)
AND
DB_LLSPRINT_SprintStatusToggled(_Character)
THEN
NOT DB_LLSPRINT_SprintStatusToggled(_Character);

IF
ObjectFlagSet("ToggleSprint_SprintingEnabled", (CHARACTERGUID)_Character, _)
AND
NOT LLSPRINT_QRY_IgnoreCharacter(_Character)
AND
NOT UserGetFlag(_Character, "ToggleSprint_DisableSprintingStatusText", 1)
AND
GlobalGetFlag("ToggleSprint_SprintingStatusHidden", 1)
THEN
CharacterStatusText(_Character, "LLSPRINT_StatusText_SprintingEnabled");

IF
ObjectFlagCleared("ToggleSprint_SprintingEnabled", (CHARACTERGUID)_Character, _)
AND
NOT UserGetFlag(_Character, "ToggleSprint_DisableSprintingStatusText", 1)
AND
NOT LLSPRINT_QRY_IgnoreCharacter(_Character)
AND
GlobalGetFlag("ToggleSprint_SprintingStatusHidden", 1)
THEN
CharacterStatusText(_Character, "LLSPRINT_StatusText_SprintingDisabled");
//END_REGION

//REGION RESURRECTED
IF
CharacterDied(_Character)
AND
ObjectGetFlag(_Character, "ToggleSprint_SprintingEnabled", 1)
THEN
ObjectSetFlag(_Character, "ToggleSprint_WasSprinting");

IF
CharacterStatusRemoved(_Character, "RESURRECT", _)
AND
ObjectGetFlag(_Character, "ToggleSprint_WasSprinting", 1)
AND
LLSPRINT_QRY_CanSprint(_Character)
THEN
ObjectClearFlag(_Character, "ToggleSprint_WasSprinting");
LLSPRINT_ToggleSprinting(_Character, 1);
//END_REGION

//REGION VERSIONING
IF
GameStarted(_Level, _)
AND
IsGameLevel(_Level, 1)
AND
LLSPRINT_Updater_QRY_UpdateNeeded("1.2.0.0")
THEN
LLSPRINT_Internal_RegisterMod();

PROC
LLSPRINT_Internal_RegisterMod()
THEN
LLSPRINT_Updater_RemoveOldVersions("1.2.0.0");
LLSPRINT_Updater_SetVersion("1.2.0.0");

QRY
LLSPRINT_Updater_QRY_UpdateNeeded((STRING)_Version)
AND
NOT DB_Mods_Registered("ToggleSprint_DefinitiveEdition", "LaughingLeader", _Version)
THEN
DB_NOOP(1);

PROC
LLSPRINT_Updater_RemoveOldVersions((STRING)_NextVersion)
AND
DB_Mods_Registered("ToggleSprint_DefinitiveEdition", "LaughingLeader", _Version)
THEN
NOT DB_Mods_Registered("ToggleSprint_DefinitiveEdition", "LaughingLeader", _Version);
LLSPRINT_Updater_VersionUpdated(_Version, _NextVersion);

PROC
LLSPRINT_Updater_VersionUpdated((STRING)_Version, (STRING)_NextVersion)
THEN
DB_NOOP(1);

PROC
LLSPRINT_Updater_VersionUpdated((STRING)_Version, "1.1.0.0")
THEN
SysClear("DB_ToggleSprint_DefaultWalkingSpeed", 1);
DB_ToggleSprint_DefaultWalkingSpeed("ToggleSprint_WalkingSpeed_00");

PROC
LLSPRINT_Updater_VersionUpdated((STRING)_Version, "1.1.0.0")
AND
DB_IsPlayer(_Player)
AND
HasActiveStatus(_Player, "LLSPRINT_TOGGLESPRINT", 1)
THEN
RemoveStatus(_Player, "LLSPRINT_TOGGLESPRINT");

PROC
LLSPRINT_Updater_SetVersion((STRING)_Version)
AND
GlobalGetFlag("LeaderLib_Initialized", 1)
THEN
//Fire LeaderLib ModUpdated procs.
DB_LeaderLib_ModApi_RegisterMod("ToggleSprint_DefinitiveEdition", "LaughingLeader", _Version);

PROC
LLSPRINT_Updater_SetVersion((STRING)_Version)
AND
NOT GlobalGetFlag("LeaderLib_Initialized", 1)
THEN
DB_Mods_Registered("ToggleSprint_DefinitiveEdition", "LaughingLeader", _Version);
//END_REGION

//REGION LEADERLIB
// [BEGIN_NO_OSITOOLS]
IF
StoryEvent(_, "LeaderLib_Initialized")
AND
NOT DB_ToggleSprint_RegisteredLeaderLibSettings(_)
THEN
DB_ToggleSprint_RegisteredLeaderLibSettings(1);

IF
DB_ToggleSprint_RegisteredLeaderLibSettings(1)
THEN
DB_LeaderLib_ModApi_RegisterActiveGoal("ToggleSprint_DefinitiveEdition", "LaughingLeader", "LLSPRINT_00_Main");
DB_LeaderLib_ModApi_RegisterMenu("LaughingLeader.ToggleSprint.DefinitiveEdition", "[Toggle Sprint] Settings", "ToggleSprint_SettingsMenu", "ToggleSprint_DefinitiveEdition", "LaughingLeader");
DB_LeaderUpdater_RegisteredUUID("ToggleSprint_DefinitiveEdition", "LaughingLeader", 3cdec0ff-5d98-e859-87cd-c663b7b9fcfe);
DB_LeaderLib_ModMenu_RegisteredMenu_OpenEvent("LaughingLeader.ToggleSprint.DefinitiveEdition", "ToggleSprint_SettingsMenu_OpenRequest");
DB_LeaderLib_Treasure_TreasureItemTemplates("LeaderLib.LeaderTrader.ModBooks", "BOOK_Book_LLSPRINT_Settings_54bb6a3e-1b5c-4e95-9f9b-06d87f3e7715", 1, "");
DB_LeaderLib_Treasure_ItemMaxAmount("LeaderLib.LeaderTrader.ModBooks", "BOOK_Book_LLSPRINT_Settings_54bb6a3e-1b5c-4e95-9f9b-06d87f3e7715", 1);

IF
StoryEvent(_, "LeaderLib_Initialized")
AND
NOT DB_LeaderLib_ModMenu_RegisteredMenu_OpenEvent("LaughingLeader.ToggleSprint.DefinitiveEdition", _)
THEN
DB_LeaderLib_ModMenu_RegisteredMenu_OpenEvent("LaughingLeader.ToggleSprint.DefinitiveEdition", "ToggleSprint_SettingsMenu_OpenRequest");
// [END_NO_OSITOOLS]

PROC
LeaderLib_Mods_OnModRegistered(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, (INTEGER)_Version, (INTEGER)_Major, (INTEGER)_Minor, (INTEGER)_Revision, (INTEGER)_Build)
THEN
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalFlag(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "ToggleSprint_AddSkillToSummons", 1);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalFlag(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "ToggleSprint_AutoAddSprintSkillDisabled", 1);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalFlag(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "ToggleSprint_CanOverridePartySpeed", 1);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalFlag(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "ToggleSprint_CombatSprintingEnabled", 1);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalFlag(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "ToggleSprint_EnemySprintingEnabled", 1);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalFlag(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "ToggleSprint_EnemyWalkSpeedOverrideEnabled", 1);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalFlag(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "ToggleSprint_FirstTimeSetupComplete");
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalFlag(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "ToggleSprint_SprintingStatusHidden", 1);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalFlag(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "ToggleSprint_LadderSprintingDisabled", 1);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalFlag(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "ToggleSprint_FasterLadderSprinting", 1);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalInteger(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "EnemySprintingSpeed", 5);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalInteger(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "EnemyWalkingSpeed", 2);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalInteger(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "PartySprintingSpeed", 5);
DB_LeaderLib_ModApi_GlobalSettings_Register_GlobalInteger(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "PartyWalkingSpeed", 2);
DB_LeaderLib_ModApi_RegisterMenu("3cdec0ff-5d98-e859-87cd-c663b7b9fcfe", "ToggleSprint_SettingsMenu", "[Toggle Sprint] Settings");
DB_LeaderLib_ModMenu_RegisteredMenu_OpenEvent("3cdec0ff-5d98-e859-87cd-c663b7b9fcfe", "ToggleSprint_SettingsMenu_OpenRequest");
/*
PROC
LeaderLib_Register_ModMenu("3cdec0ff-5d98-e859-87cd-c663b7b9fcfe", (STRING)_Dialog, (STRING)_DisplayName)
AND
DB_LeaderLib_ModMenu_RegisteredMenuData("LaughingLeader.ToggleSprint.DefinitiveEdition", "[Toggle Sprint] Settings", "ToggleSprint_SettingsMenu", "ToggleSprint_DefinitiveEdition", "LaughingLeader")
THEN
NOT DB_LeaderLib_ModMenu_RegisteredMenuData("LaughingLeader.ToggleSprint.DefinitiveEdition", "[Toggle Sprint] Settings", "ToggleSprint_SettingsMenu", "ToggleSprint_DefinitiveEdition", "LaughingLeader");
*/
//END_REGION

//REGION LEADERLIB_GLOBAL_SETTINGS
IF
GlobalFlagSet("LLSPRINT_SaveGlobalSettings")
THEN
GlobalClearFlag("LLSPRINT_SaveGlobalSettings");
LLSPRINT_SaveGlobalSettings();

PROC
LLSPRINT_SaveGlobalSettings()
AND
GlobalGetFlag("LeaderLib_Initialized", 1)
AND
GlobalGetFlag("LeaderLib_ExtenderIsActive", 1)
AND
CharacterGetHostCharacter(_Host)
THEN
SetStoryEvent(_Host, "LeaderLib_Commands_SaveGlobalSettings");

PROC
LeaderLib_GlobalSettings_GetIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "PartySprintingSpeed")
AND
DB_ToggleSprint_PartySprintingSpeed(_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _Speed)
AND
Integer(_Speed, _SpeedInt)
THEN
DB_LeaderLib_ModApi_GlobalSettings_SaveGlobalIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "PartySprintingSpeed", _SpeedInt);

PROC
LeaderLib_GlobalSettings_SetIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "PartySprintingSpeed", (INTEGER)_Speed)
AND
IntegerMax(_Speed, 2, _ClampedA)
AND
IntegerMin(_Speed, 11, _ClampedB)
AND
Real(_ClampedB, _SpeedR)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedR)
THEN
LLSPRINT_SetPartySpeed(_Flag);

//If users assign a new speed
/*
PROC
LeaderLib_GlobalSettings_SetIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "PartySprintingSpeed", (INTEGER)_Speed)
AND
IntegerMax(_Speed, 1, _ClampedA)
AND
IntegerMin(_Speed, 999, _ClampedB)
AND
Real(_ClampedB, _SpeedR)
AND
NOT DB_ToggleSprint_SprintingSpeed(_, _, _SpeedR)
AND
SysCount("DB_ToggleSprint_SprintingSpeed", 3, _Count)
AND
IntegerSubtract(_Count, 1, _FlagInt)
AND
IntegertoString(_FlagInt, _FlagIntStr)
AND
StringConcatenate("ToggleSprint_SelectSpeed_", _FlagIntStr, _NewSelectFlag)
AND
StringConcatenate("ToggleSprint_SprintingSpeed_", _FlagIntStr, _NewFlag)
THEN
DB_ToggleSprint_SprintingSpeed(_NewSelectFlag, _NewFlag, _SpeedR);
*/

PROC
LeaderLib_GlobalSettings_GetIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "PartyWalkingSpeed")
AND
DB_ToggleSprint_PartyWalkingSpeed(_Flag)
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _Speed)
AND
Integer(_Speed, _SpeedInt)
THEN
DB_LeaderLib_ModApi_GlobalSettings_SaveGlobalIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "PartyWalkingSpeed", _SpeedInt);

PROC
LeaderLib_GlobalSettings_SetIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "PartyWalkingSpeed", (INTEGER)_Speed)
AND
IntegerMax(_Speed, 2, _ClampedA)
AND
IntegerMin(_Speed, 9, _ClampedB)
AND
Real(_ClampedB, _SpeedR)
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedR)
THEN
LLSPRINT_SetPartyWalkSpeed(_Flag);

PROC
LeaderLib_GlobalSettings_GetIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "EnemySprintingSpeed")
AND
DB_ToggleSprint_EnemySprintingSpeed(_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _Speed)
AND
Integer(_Speed, _SpeedInt)
THEN
DB_LeaderLib_ModApi_GlobalSettings_SaveGlobalIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "EnemySprintingSpeed", _SpeedInt);

PROC
LeaderLib_GlobalSettings_SetIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "EnemySprintingSpeed", (INTEGER)_Speed)
AND
IntegerMax(_Speed, 2, _ClampedA)
AND
IntegerMin(_Speed, 11, _ClampedB)
AND
Real(_ClampedB, _SpeedR)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedR)
THEN
SysClear("DB_ToggleSprint_EnemySprintingSpeed", 1);
DB_ToggleSprint_EnemySprintingSpeed(_Flag);

PROC
LeaderLib_GlobalSettings_GetIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "EnemyWalkingSpeed")
AND
DB_ToggleSprint_EnemyWalkingSpeed(_Flag)
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _Speed)
AND
Integer(_Speed, _SpeedInt)
THEN
DB_LeaderLib_ModApi_GlobalSettings_SaveGlobalIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "EnemyWalkingSpeed", _SpeedInt);

PROC
LeaderLib_GlobalSettings_SetIntegerVariable(3cdec0ff-5d98-e859-87cd-c663b7b9fcfe, "EnemyWalkingSpeed", (INTEGER)_Speed)
AND
IntegerMax(_Speed, 2, _ClampedA)
AND
IntegerMin(_Speed, 9, _ClampedB)
AND
Real(_ClampedB, _SpeedR)
AND
DB_ToggleSprint_WalkingSpeed(_SelectFlag, _Flag, _SpeedR)
THEN
SysClear("DB_ToggleSprint_EnemyWalkingSpeed", 1);
DB_ToggleSprint_EnemyWalkingSpeed(_Flag);
//END_REGION

//REGION SAFETY_CHECKS
PROC
LLSPRINT_Updater_VersionUpdated((STRING)_Version, (STRING)_NextVersion)
THEN
IterateParties("ToggleSprintDE_Iterator_CheckPlayers");

IF
StoryEvent((CHARACTERGUID)_Player, "ToggleSprintDE_Iterator_CheckPlayers")
AND
NOT QRY_IsSummonOrPartyFollower(_Player)
AND
NOT LLSPRINT_QRY_SpeedVariableSet(_Player)
AND
DB_ToggleSprint_PartySprintingSpeed(_Flag)
AND
DB_ToggleSprint_SprintingSpeed(_SelectFlag, _Flag, _SpeedVal)
THEN
LLSPRINT_SetSpeed(_Player, _SpeedVal);
SetVarString(_Player, "ToggleSprint_CurrentSpeedFlag", _Flag);

IF
StoryEvent((CHARACTERGUID)_Player, "ToggleSprintDE_Iterator_CheckPlayers")
AND
GlobalGetFlag("ToggleSprint_AutoAddSprintSkillDisabled", 0)
AND
NOT QRY_IsSummonOrPartyFollower(_Player)
AND
NOT LLSPRINT_QRY_IgnoreCharacter(_Player)
AND
CharacterHasSkill(_Player, "Shout_LLSPRINT_ToggleSprint", 0)
AND
NOT ObjectGetFlag(_Player, "LLSPRINT_Debug_RemovedSprintSkill", 1) // In case it was removed manually by the player.
THEN
CharacterAddSkill(_Player, "Shout_LLSPRINT_ToggleSprint", 0);

IF
StoryEvent((CHARACTERGUID)_Player, "ToggleSprintDE_Iterator_CheckPlayers")
AND
GlobalGetFlag("LeaderLib_AutoAddModMenuBooksDisabled", 0)
AND
NOT QRY_IsSummonOrPartyFollower(_Player)
AND
ItemTemplateIsInUserInventory(_Player, "BOOK_Book_LLSPRINT_Settings_54bb6a3e-1b5c-4e95-9f9b-06d87f3e7715", 0, _Count)
AND
_Count < 1
THEN
ItemTemplateAddTo("BOOK_Book_LLSPRINT_Settings_54bb6a3e-1b5c-4e95-9f9b-06d87f3e7715", _Player, 1, 0);
//END_REGION

//REGION UPDATES
PROC
LLSPRINT_Updater_VersionUpdated((STRING)_Version, "1.0.2.0")
AND
GlobalGetFlag("ToggleSprint_AddSkillToSummons", 1)
AND
DB_IsPlayer(_Player)
THEN
UserSetFlag(_Player, "ToggleSprint_AddSkillToSummons", 0);

PROC
LLSPRINT_Updater_VersionUpdated((STRING)_Version, "1.0.2.0")
AND
GlobalGetFlag("ToggleSprint_AddSkillToSummons", 1)
THEN
GlobalClearFlag("ToggleSprint_AddSkillToSummons");
//END_REGION

//REGION DEBUG
/*
IF
StoryEvent((CHARACTERGUID)_Player, "ToggleSprint_Events_SetInitialSprinting")
AND
DB_CurrentLevel("_TMPL_Sandbox")
AND
NOT DB_IsPlayer(_Player)
THEN
DB_IsPlayer(_Player);
*/

IF
TextEventSet("togglesprint_openmenu")
AND
CharacterGetHostCharacter(_Host)
THEN
UserSetFlag(_Host, "LLSPRINT_IsHost");
Proc_StartDialog(0, "ToggleSprint_SettingsMenu", _Host, _Host);
//END_REGION

//REGION GLOBAL_SETTINGS_SPRINTING_SKILL
IF
GlobalFlagSet("ToggleSprint_AutoAddSprintSkillDisabled")
AND
NOT DB_DialogName("ToggleSprint_SettingsMenu", _)
THEN
IterateParties("LLSPRINT_Iterator_RemoveSprintingSkill");

IF
StoryEvent((CHARACTERGUID)_Char, "LLSPRINT_Iterator_RemoveSprintingSkill")
AND
CharacterHasSkill(_Char, "Shout_LLSPRINT_ToggleSprint", 1)
THEN
CharacterRemoveSkill(_Char, "Shout_LLSPRINT_ToggleSprint");

IF
GlobalFlagCleared("ToggleSprint_AutoAddSprintSkillDisabled")
AND
NOT DB_DialogName("ToggleSprint_SettingsMenu", _)
THEN
IterateParties("LLSPRINT_Iterator_AddSprintingSkill");

IF
StoryEvent((CHARACTERGUID)_Char, "LLSPRINT_Iterator_AddSprintingSkill")
AND
NOT QRY_IsSummonOrPartyFollower(_Char)
AND
NOT LLSPRINT_QRY_IgnoreCharacter(_Char)
AND
CharacterHasSkill(_Char, "Shout_LLSPRINT_ToggleSprint", 0)
AND
NOT ObjectGetFlag(_Char, "LLSPRINT_Debug_RemovedSprintSkill", 1) // In case it was removed manually by the player.
THEN
CharacterAddSkill(_Char, "Shout_LLSPRINT_ToggleSprint", 0);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_ToggleSprintDE"
