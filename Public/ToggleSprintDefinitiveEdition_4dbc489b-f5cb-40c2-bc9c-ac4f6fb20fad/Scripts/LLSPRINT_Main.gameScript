INIT
	
EVENTS

EVENT LLSPRINT_PrintReal
VARS
	CHARACTER:_Character
	FLOAT:_RealVar
	STRING:_Str
	FIXEDSTRING:_FStr
ON
	OnCharacterEvent(_Character, "LLSPRINT_Events_Debug_PrintReal")
ACTIONS
IF "c1"
	GetVar(_RealVar, _Character, "LLSPRINT_Debug_RealVar")
THEN
	Print(_Str, "Hitpoints Percentage: [1]", _RealVar)
	Cast(_FStr, _Str)
	DisplayCombatInfoText(_Character, _FStr, 10.0)
ENDIF

EVENT LLSPRINT_ToggleInfoStatus
ON
	OnGlobalFlagCleared("ToggleSprint_SprintingStatusHidden")
	OnGlobalFlagSet("ToggleSprint_SprintingStatusHidden")
ACTIONS
	IterateParty("ToggleSprint_Iterator_ToggleInfoStatus")

EVENT LLSPRINT_SprintInfoStatus
VARS
	CHARACTER:_Character
ON
	OnCharacterFlagSet("ToggleSprint_EnemySprintEnabled", _Character)
	OnIterateCharacter(_Character, "ToggleSprint_Iterator_ToggleInfoStatus")
ACTIONS
IF "c1"
	CharacterIsPlayer(_Character)
THEN
	IF "!c1"
		HasGlobalFlag("ToggleSprint_SprintingStatusHidden")
	THEN
		CharacterApplyStatus(_Character, LLSPRINT_TOGGLESPRINT_INFO, -1)
	ELSE
		IF "c1"
			CharacterHasStatus(_Character, LLSPRINT_TOGGLESPRINT_INFO)
		THEN
			CharacterRemoveStatus(_Character, LLSPRINT_TOGGLESPRINT_INFO, null, 0)
		ENDIF
	ENDIF
ENDIF

EVENT LLSPRINT_RemoveSprintInfoStatus
VARS
	CHARACTER:_Character
ON
	OnCharacterFlagCleared("ToggleSprint_EnemySprintEnabled", _Character)
ACTIONS
IF "c1"
	CharacterHasStatus(_Character, LLSPRINT_TOGGLESPRINT_INFO)
THEN
	CharacterRemoveStatus(_Character, LLSPRINT_TOGGLESPRINT_INFO, null, 0)
ENDIF

//REGION SET_SPEEDS
EVENT LLSPRINT_SetSpeed_Sprinting
VARS
	CHARACTER:_Character
	FLOAT:_Speed
ON
	OnCharacterFlagSet("ToggleSprint_SprintingEnabled", _Character)
	OnCharacterFlagSet("ToggleSprint_EnemySprintEnabled", _Character)
	OnCharacterEvent(_Character, "ToggleSprint_Commands_UpdateSpeed")
ACTIONS
IF "c1"
	GetVar(_Speed, _Character, "ToggleSprint_SprintSpeed")
THEN
	CharacterSetRunSpeedOverride(_Character, 1, _Speed)
ELSE
	CharacterSetRunSpeedOverride(_Character, 0)
ENDIF

EVENT LLSPRINT_ClearSpeed_Sprinting
VARS
	CHARACTER:_Character
ON
	OnCharacterFlagCleared("ToggleSprint_SprintingEnabled", _Character)
	OnCharacterFlagCleared("ToggleSprint_EnemySprintEnabled", _Character)
ACTIONS
	CharacterSetRunSpeedOverride(_Character, 0)
	
EVENT LLSPRINT_SetSpeed_Walking
VARS
	CHARACTER:_Character
	FLOAT:_Speed
ON
	OnCharacterFlagSet("ToggleSprint_WalkSpeedOverrideEnabled", _Character)
ACTIONS
IF "c1"
	GetVar(_Speed, _Character, "ToggleSprint_WalkSpeed")
THEN
	CharacterSetWalkSpeedOverride(_Character, 1, _Speed)
ELSE
	CharacterSetWalkSpeedOverride(_Character, 0)
ENDIF

EVENT LLSPRINT_ClearSpeed_Walking
VARS
	CHARACTER:_Character
ON
	OnCharacterFlagCleared("ToggleSprint_WalkSpeedOverrideEnabled", _Character)
ACTIONS
	CharacterSetWalkSpeedOverride(_Character, 0)	
//END_REGION

//REGION COMBAT
EVENT LLSPRINT_EnteredCombat_Player
VARS
	CHARACTER:_Character
ON
	OnEnteredCombat(_Character, _)
ACTIONS
IF "!c1&c2"
	HasGlobalFlag("ToggleSprint_CombatSprintingEnabled")
	HasFlag(_Character, "ToggleSprint_SprintingEnabled")
THEN
	SetFlag(_Character, "ToggleSprint_WasSprinting")
	ClearFlag(_Character, "ToggleSprint_SprintingEnabled")
ENDIF

EVENT LLSPRINT_LeftCombat
VARS
	CHARACTER:_Character
ON
	OnLeftCombat(_Character, _)
ACTIONS
IF "c1"
	HasFlag(_Character, "ToggleSprint_WasSprinting")
THEN
	ClearFlag(_Character, "ToggleSprint_WasSprinting")
	SetFlag(_Character, "ToggleSprint_SprintingEnabled")
ENDIF

EVENT LLSPRINT_CombatFlagSet
ON
	OnGlobalFlagSet("ToggleSprint_CombatSprintingEnabled")
ACTIONS
	IterateParty("ToggleSprint_Iterator_EnableSprinting")

EVENT LLSPRINT_CombatFlagSet_Iterator
VARS
	CHARACTER:_Character
ON
	OnIterateCharacter(_Character, "ToggleSprint_Iterator_EnableSprinting")
ACTIONS
IF "c1"
	HasFlag(_Character, "ToggleSprint_WasSprinting")
THEN
	ClearFlag(_Character, "ToggleSprint_WasSprinting")
	SetFlag(_Character, "ToggleSprint_SprintingEnabled")
ENDIF

EVENT LLSPRINT_CombatFlagCleared
ON
	OnGlobalFlagCleared("ToggleSprint_CombatSprintingEnabled")
ACTIONS
	IterateParty("ToggleSprint_Iterator_DisableSprinting")

EVENT LLSPRINT_CombatFlagCleared_Iterator
VARS
	CHARACTER:_Character
ON
	OnIterateCharacter(_Character, "ToggleSprint_Iterator_DisableSprinting")
ACTIONS
IF "c1&c2"
	HasFlag(_Character, "ToggleSprint_SprintingEnabled")
	IsInCombat(_Character)
THEN
	SetFlag(_Character, "ToggleSprint_WasSprinting")
	ClearFlag(_Character, "ToggleSprint_SprintingEnabled")
ENDIF

//END_REGION